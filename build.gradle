plugins {
    id 'me.champeau.jmh' version '0.7.3'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

defaultTasks 'build'

group = 'org.pcollections'
version = '5.0.0-SNAPSHOT'

description = """PCollections"""

repositories {
    mavenCentral()
}

dependencies {
    // Use JUnit 5: JUnit Jupiter + JUnit Vintage [for running JUnit 3/4 tests as well]
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.0"
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

test {
    useJUnitPlatform() // Use JUnit 5
}

java {
    toolchain {
        // our code works with Java 8, but module-info.java only works with Java 9+
        // we used to use `modularity.mixedJavaRelease 8` to deal with this,
        // but Java 8 is old enough now that simpler build process wins
        languageVersion = JavaLanguageVersion.of(9)
    }
    withSourcesJar()
    withJavadocJar()
}

task copyJavadocDocFiles(type: Copy) {
    from('src/main/java')
    into 'build/docs/javadoc'
    include '**/doc-files/*.*'
}

javadoc {
    dependsOn copyJavadocDocFiles // https://github.com/gradle/gradle/issues/4046
    options.addBooleanOption('Xdoclint:none', true) // https://github.com/hrldcpr/pcollections/issues/62
}

publishing {
  repositories {
    maven {
      url = layout.buildDirectory.dir("repo")
    }
  }
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifactId = 'pcollections'

      pom {
        name = 'PCollections'
        packaging = 'jar'
        description = 'A Persistent Java Collections Library'
        url = 'https://github.com/hrldcpr/pcollections'

        scm {
          connection = 'scm:git:git://github.com/hrldcpr/pcollections.git'
          developerConnection = 'scm:git:ssh://github.com:hrldcpr/pcollections.git'
          url = 'https://github.com/hrldcpr/pcollections'
        }

        licenses {
          license {
            name = 'The MIT License'
            url = 'https://opensource.org/licenses/mit-license.php'
          }
        }

        developers {
          developer {
            id = 'hrldcpr'
            name = 'Harold Cooper'
            email = 'hrldcpr@gmail.com'
          }
        }
      }
    }
  }
}

signing {
    sign(publishing.publications.mavenJava)
}
